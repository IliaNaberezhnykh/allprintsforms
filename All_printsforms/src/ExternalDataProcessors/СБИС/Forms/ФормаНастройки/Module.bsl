
&НаКлиенте
Перем НажатиеСохранитьВыполнено;
&НаКлиенте
Перем КлючиИспользовать;
&НаКлиенте
Перем НастройкиПодключения_Было;
&НаКлиенте
Перем МестныйКэш Экспорт;

#Область include_local_ПолучитьМодульОбъекта

&НаСервере
Функция МодульОбъектаСервер()
	Возврат РеквизитФормыВЗначение("Объект");
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент() Экспорт
	Возврат ВладелецФормы.Кэш.СБИС.МодульОбъектаКлиент;
КонецФункции

#КонецОбласти

#Область include_core_vo2_Формы_ФормаНастройки

#Область include_core_vo2_Формы_ФормаНастройки_Интерфейс_Вызов

&НаКлиенте 
Процедура Показать(ПараметрыОткрытия) Экспорт

	СбисУстановитьФорму(ПараметрыОткрытия);
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Результат = ОткрытьМодально();
		Если Не ОписаниеОповещенияОЗакрытии = Неопределено Тогда
			МодульОбъектаКлиент().ВыполнитьСбисОписаниеОповещения(Результат, ОписаниеОповещенияОЗакрытии);
		КонецЕсли;
	#Иначе
		Открыть();
	#КонецЕсли

КонецПроцедуры

#КонецОбласти

#Область include_core_vo2_Формы_ФормаНастройки_Интерфейс_События_Форма

&НаКлиенте
Процедура ПриОткрытии()
	
	НажатиеСохранитьВыполнено = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если РежимЗапуска = "НастройкиСоединения" И НажатиеСохранитьВыполнено Тогда//Пробуем включить обмен
		//Нажали закрыть, возвращаем как было и закрываем.
		ДополнительныеПараметры	= Новый Структура("ФормаВладелец, ВызыватьРекурсивно", ЭтаФорма, Ложь);//Для корректного сообщения об ошибке УФ
		НастройкиПодключения	= Новый Структура(КлючиИспользовать);
		ЗаполнитьЗначенияСвойств(НастройкиПодключения, Этаформа);
		Изменено = МестныйКэш.ГлавноеОкно.ОпределитьИнтеграциюРабочиеФормы(МестныйКэш, НастройкиПодключения, ДополнительныеПараметры);

		Если Изменено Тогда
			МестныйКэш.Интеграция.СбисУстановитьВремяОжидания(МестныйКэш, ВремяОжиданияОтвета);
		Иначе
			НажатиеСохранитьВыполнено = Ложь;
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОбработчикЗавершения = МодульОбъектаКлиент().НовыйСбисОписаниеОповещения("ПослеЗакрытия", ЭтаФорма);
	НовыйОтложенноеДействие = МодульОбъектаКлиент().НовыйОтложенноеДействие(Новый Структура("ОписаниеОповещения", ОбработчикЗавершения));
	МодульОбъектаКлиент().ПодключитьОтложенноеДействие(НовыйОтложенноеДействие);
	
КонецПроцедуры

// Процедура - после закрытия почистить форму
//
// Параметры:
//  Аргумент	 - 	 - 
//  ДопПараметры - 	 - 
//
&НаКлиенте
Процедура ПослеЗакрытия(Аргумент=Неопределено, ДопПараметры=Неопределено) Экспорт
	
	ОписаниеОповещенияОЗакрытии = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область include_core_vo2_Формы_ФормаНастройки_Интерфейс_События_Элементы

&НаКлиенте
Процедура ВремяОжиданияОтветаПриИзменении(Элемент)
	МестныйКэш.Интеграция.СбисУстановитьВремяОжидания(МестныйКэш, ВремяОжиданияОтвета);
	ПараметрыПриИзменении(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура КаталогНастроекНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка= Ложь;
	КаталогНастроек	= сбисВыбратьКаталог(КаталогНастроек);

КонецПроцедуры

&НаКлиенте
Процедура КаталогНастроекОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	МестныйКэш.ГлавноеОкно.сбисОткрытьКаталог(КаталогНастроек);
КонецПроцедуры 

&НаКлиенте
Процедура КаталогНастроекПриИзменении(Элемент = Неопределено) Экспорт
	
	МестныйКэш.ГлавноеОкно.ПараметрыПриИзменении(МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "КаталогНастроек"));
	МестныйКэш.Парам.ИдентификаторНастроек = МестныйКэш.ФормаНастроек.сбисПолучитьИдентификаторНастроек(МестныйКэш);		
	ВыполнитьПроверкуНастроек();
	
КонецПроцедуры 

//открывает диалог выбора каталога обмена	
&НаКлиенте
Процедура КаталогОбменаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ДиалогОткрытия = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога); 
	ДиалогОткрытия.Заголовок = "Выберите каталог обмена документами"; 
	Если ДиалогОткрытия.Выбрать() Тогда 
		КаталогОбмена = ДиалогОткрытия.Каталог; 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НастройкаЭПНажатие(Элемент)
	фрм = МестныйКэш.ГлавноеОкно.сбисПолучитьФорму("ФормаНастройкиПодписания",,,МестныйКэш.ГлавноеОкно);
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		МестныйКэш.ГлавноеОкно.сбисПослеНастройкиЭП(фрм.ОткрытьМодально(), Неопределено); 
	#Иначе
		фрм.ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("сбисПослеНастройкиЭП",МестныйКэш.ГлавноеОкно);
		фрм.Открыть();
	#КонецЕсли
КонецПроцедуры

// Процедура устанавливает видимость элементов формы в зависимости от выбранного варианта настроек прокси	
&НаКлиенте
Процедура НастройкиПроксиСервераПриИзменении(Элемент)
	
	ОбновитьДоступностьНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособОбменаПриИзменении(Элемент)
	
	Если		СпособОбмена = 1
		И	Не	ЗначениеЗаполнено(КаталогОбмена) Тогда
		КаталогОбменаНачалоВыбора(МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "КаталогОбмена"),"", Истина);
	КонецЕсли;
	ОбновитьДоступностьНастроек();

	// При изменении способа обмена (SDK, API, каталог)	"перезапускаем" обработку
	ПараметрыИнтеграции = Новый Структура("АдресСервера, СпособОбмена, СпособХраненияНастроек, ВремяОжиданияОтвета", МестныйКэш.СБИС.АдресСервера);
	ЗаполнитьЗначенияСвойств(ПараметрыИнтеграции, МестныйКэш.Парам);
	ПараметрыИнтеграции.СпособОбмена = СпособОбмена;
	МестныйКэш.ГлавноеОкно.ПерезапуститьГлавноеОкно(ПараметрыИнтеграции, , Ложь);
	ПараметрыПриИзменении(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособХраненияНастроекПриИзменении(Элемент)

	ОбновитьДоступностьНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеЭДПриИзменении(Элемент)
	// Включает/выключает дублирование статусов в типовые регистры 1С
	Если СостояниеЭД = Истина Тогда
		ПараметрыПоиска = Новый Структура;
		ПараметрыПоиска.Вставить("ИмяФункции",	"ДублироватьСостояние");
		ПараметрыПоиска.Вставить("КлючФорм",	"Статусы_СостоянияЭД");
		Отказ = Ложь;
		ПараметрыПодсистемы = МестныйКэш.ОбщиеФункции.сбисИнициироватьПодсистему(МестныйКэш, ПараметрыПоиска, Отказ);
		Если Отказ Тогда
			Сообщить("Дублирование статусов в типовые регистры 1С не поддерживается для Вашей конфигурации 1С: "+ПараметрыПодсистемы.details);
			СостояниеЭД = Ложь;
		Иначе
			ПараметрыПриИзменении(Элемент);
		КонецЕсли;
	Иначе
		ПараметрыПриИзменении(Элемент);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СпособХраненияМетокСтатусовПриИзменении(Элемент)
	Если СпособХраненияМетокСтатусов = МестныйКэш.Парам.СпособХраненияМетокСтатусов Тогда
		Возврат
	КонецЕсли;
	
	Попытка
		МодульОбъектаКлиент().СохранитьМеткиСтатусов(МестныйКэш);
		ПараметрыПриИзменении(Элемент);
		МеткиСтатусов = МестныйКэш.ФормаНастроек.СбисПрочитатьМеткиСтатусов(МестныйКэш);
		ЗаполнитьЗначенияСвойств(ЭтаФорма, МеткиСтатусов);
		ЗаполнитьЗначенияСвойств(МестныйКэш.ГлавноеОкно, МеткиСтатусов);
		МестныйКэш.ФормаНастроек.ИнициализироватьКэшНастроек(МестныйКэш, Новый Структура("Обновить, Параметр", Истина, "status_marks")); //для способа хранения меток в разрезе аккаунта добавим status_marks в список параметров, хранящихся на шаблоне	
	Исключение
		ИнфаОбОшибке = ИнформацияОбОшибке();
		МодульОбъектаКлиент().СообщитьСбисИсключение(ИнфаОбОшибке, "ФормаНастройки.СпособХраненияМетокСтатусовПриИзменении")
	КонецПопытки;
КонецПроцедуры

// << alo Меркурий
&НаКлиенте
Процедура МеркурийПриИзменении(Элемент=Неопределено) Экспорт
	ПараметрыПриИзменении(МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "Меркурий"));
	
	СписокДокументов = Новый СписокЗначений;
	Для Каждого Ини Из МестныйКэш.Ини Цикл
		ЗначениеИни = МестныйКэш.ФормаНастроек.Ини(МестныйКэш, Ини.Ключ);
		Если ЗначениеИни.Свойство("мФайл") И ЗначениеИни.мФайл.Свойство("АктРасхождение") Тогда
			СписокДокументов.Добавить(Ини.Ключ);
		КонецЕсли;
	КонецЦикла;
	Если ЗначениеЗаполнено(СписокДокументов) Тогда
		ИмяФайлаОбработки = МестныйКэш.ПараметрыСистемы.Обработка.ПолноеИмяОбработки;
		ВидДопОбработокПечатнаяФорма = неопределено;
		Если МестныйКэш.Ини.Конфигурация.Свойство("ВидДопОбработокПечатнаяФорма") Тогда
			ВидДопОбработокПечатнаяФорма = МестныйКэш.ОбщиеФункции.РассчитатьЗначениеНаСервере("ВидДопОбработокПечатнаяФорма", Новый Структура("Ини", МестныйКэш.Ини.Конфигурация));
		КонецЕсли;
		ПараметрыФормированияНаСервере = Новый Структура("ПометкаУдаления, ПредставлениеПФ, ИдентификаторКоманды, УправляемоеПриложение, ВидДопОбработокПечатнаяФорма, СписокДокументов", 
		(Не Меркурий), "Погасить ВСД", "sbis1cПогаситьВСД", МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение, ВидДопОбработокПечатнаяФорма, СписокДокументов);
		Попытка
			Если МестныйКэш.ПараметрыСистемы.Обработка.РежимИспользования = "Файл внешней обработки" Тогда
				ОбработкаХранитсяВСправочнике = Ложь;
				ДвоичныеДанныеОбработки = Новый ДвоичныеДанные(ИмяФайлаОбработки);
			Иначе
				ОбработкаХранитсяВСправочнике = Истина;
				ДвоичныеДанныеОбработки = Неопределено;
			КонецЕсли;
			ДанныеОбработки  = Новый Структура("ДвоичныеДанныеОбработки, ОбработкаХранитсяВСправочнике", ДвоичныеДанныеОбработки, ОбработкаХранитсяВСправочнике);
			МестныйКэш.ФормаНастроекОбщее.сбисДобавитьПечатныеФормыНаСервере(ДанныеОбработки, ПараметрыФормированияНаСервере);
			МестныйКэш.ФормаНастроекОбщее.СбисДобавитьКомандыПечатиНаФормы(МестныйКэш,, ПараметрыФормированияНаСервере);	
		Исключение
			РезультатОбновления = МестныйКэш.ОбщиеФункции.СбисИсключение(, "ФайлыНастроекОбщее.СбисДобавитьПечатныеФормы", 700, "Неизвестная ошибка подключения", ОписаниеОшибки());
			СбисПараметрыСтатистики = Новый Структура("Действие, Ошибка", "Запись ошибки", РезультатОбновления);
			МестныйКэш.ОбщиеФункции.сбисСтатистика_СформироватьИЗаписатьСтатистикуНаСервис(МестныйКэш, СбисПараметрыСтатистики, Ложь);
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры // alo Меркурий >> 

&НаКлиенте
Процедура ИспользоватьГенераторПриИзменении(Элемент)
	
	ПараметрыПриИзменении(Элемент);
	МестныйКэш.ФормаНастроек.ИспользоватьГенераторПриИзменении(МестныйКэш, ИспользоватьГенератор);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусыВГосСистемеПриИзменении(Элемент) 
	
	ПараметрыПриИзменении(Элемент);
	МестныйКэш.Текущий.Форма.НастроитьКолонки(МестныйКэш);
	
КонецПроцедуры
//Процедура записывает в местный и глобальный кэш измененный параметр	
&НаКлиенте
Процедура ПараметрыПриИзменении(Элемент) Экспорт
	
	ПутьКДаннымФормы = Сред(Элемент.Имя, Найти(Элемент.Имя, "_") + 1);
	МестныйКэш.Парам[ПутьКДаннымФормы] = ЭтаФорма[ПутьКДаннымФормы]; 
		
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыСтатусовПриИзменении(Элемент) Экспорт
	
	Если	МестныйКэш.ГлавноеОкно.ДатаПоследнегоЗапросаСтатусов = ДатаПоследнегоЗапросаСтатусов
		И	МестныйКэш.ГлавноеОкно.ИдентификаторПоследнегоСобытия = ИдентификаторПоследнегоСобытия Тогда
		Возврат;
	КонецЕсли;
		
	МестныйКэш.ГлавноеОкно.ДатаПоследнегоЗапросаСтатусов = ДатаПоследнегоЗапросаСтатусов;
	МестныйКэш.ГлавноеОкно.ИдентификаторПоследнегоСобытия = ИдентификаторПоследнегоСобытия;
	
	МодульОбъектаКлиент().СохранитьМеткиСтатусов(МестныйКэш);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалятьПрефиксыПриИзменении(Элемент)
	
	ПараметрыПриИзменении(Элемент);  
	ПараметрыОчистки = Новый Структура("Тип, Ключ", "ПользовательскиеЗначения.Функции", "НомерДокумента");
	МестныйКэш.ОбщиеФункции.сбисОчиститьЗначениеРассчитанногоОбъекта(МестныйКэш, ПараметрыОчистки);

КонецПроцедуры

&НаКлиенте
Процедура ШифроватьВыборочноПриИзменении(Элемент)
	
	ПараметрыПриИзменении(Элемент);
	Отказ = Ложь;   
	ПередаваемыеПараметры = Новый Структура("СообщатьПриОшибке, ВернутьОшибку", Ложь, Ложь);
	МестныйКэш.Интеграция.ПолучитьНастройкиПлагина(МестныйКэш, ПередаваемыеПараметры, Отказ);

КонецПроцедуры

 &НаКлиенте
Процедура АдресСервисаОбновленийПриИзменении(Элемент)
	
	//+++ МАИ 09.09.2021 Переопределяем сервер обновлений, если пользователь в режиме отладки указал другой
	МестныйКэш.СБИС.ПараметрыИнтеграции.Вставить("АдресСервисаОбновлений", АдресСервисаОбновлений);
	//--- МАИ 09.09.2021

КонецПроцедуры

&НаКлиенте
Процедура РежимОтладкиПриИзменении(Элемент)
	
	Если РежимОтладки Тогда
		КаталогОтладки = ?(ЗначениеЗаполнено(КаталогОтладки), КаталогОтладки, КаталогНастроек);	
	КонецЕсли;
	
	Если НЕ МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "НадписьАдресСервисаОбновлений") = Неопределено Тогда 
		МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "НадписьАдресСервисаОбновлений").Видимость = РежимОтладки;   
	КонецЕсли;	
	МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "АдресСервисаОбновлений").Видимость = РежимОтладки;
	
	КаталогИзменен = МестныйКэш.Интеграция.УстановитьКаталогОтладки(МестныйКэш);
	сбисПереключитьОтладку();

КонецПроцедуры

&НаКлиенте
Процедура КаталогОтладкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	КаталогОтладки = сбисВыбратьКаталог(КаталогОтладки);
	КаталогИзменен = МестныйКэш.Интеграция.УстановитьКаталогОтладки(МестныйКэш);
	
	Если КаталогИзменен Тогда
		сбисПереключитьОтладку();	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КаталогОтладкиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	МестныйКэш.ГлавноеОкно.сбисОткрытьКаталог(КаталогОтладки);

КонецПроцедуры

&НаКлиенте
Процедура КаталогОтладкиПриИзменении(Элемент)
	
	КаталогИзменен = МестныйКэш.Интеграция.УстановитьКаталогОтладки(МестныйКэш);
	
	Если КаталогИзменен Тогда
		сбисПереключитьОтладку();	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Таблица_СервисВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	//alo
	ТекущиеДанныеСтроки = ВыбраннаяСтрока;
	Если МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
		ТекущиеДанныеСтроки = Элемент.ТекущиеДанные;
	КонецЕсли;
	Контекст = Новый структура(ТекущиеДанныеСтроки.Ключ, ТекущиеДанныеСтроки.Команда);
	Контекст.Вставить("Кэш", МестныйКэш);
	МестныйКэш.ОбщиеФункции.РассчитатьЗначение(ТекущиеДанныеСтроки.Ключ, Контекст, МестныйКэш);
КонецПроцедуры

#КонецОбласти

#Область include_core_vo2_Формы_ФормаНастройки_Интерфейс_Кнопки

 //Нажатие на кнопку обновления обработки
&НаКлиенте
Процедура ОбновитьОбработку(Команда)
	
	СбисПроверитьНаличиеОбновлений(Новый Структура("Режим", "Ручной"));	
	
КонецПроцедуры	

&НаКлиенте
Процедура ПрименитьНажатие(Команда)

	СбисПрименитьНастройки();
	
КонецПроцедуры

#КонецОбласти

#Область include_core_vo2_Формы_ФормаНастройки_Интерфейс_ОбработчикиСобытий

&НаКлиенте
Процедура ВыполнитьПроверкуНастроек()
	
	МестныйКэш.ФормаНастроек.ПараметрыРаботы.Вставить("ВыполнитьПроверку", Истина);
	ПараметрыУстановки = Новый Структура("ПринудительнаяПроверка, ПродолжитьУстановку", Истина, Истина);
	МестныйКэш.ФормаНастроекОбщее.СбисПолучитьИУстановитьНастройкиВКэш(ПараметрыУстановки, МестныйКэш);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВкладкуСервис()
	
	ГруппаНастройкиСтраницы = МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "НастройкиСтраницы");
	ГруппаНастройкиСтраницы.ТекущаяСтраница = МестныйКэш.ГлавноеОкно.сбисПолучитьСтраницу(ГруппаНастройкиСтраницы, "Сервис");

	Если Не ЭтаФорма.Таблица_Сервис.Количество() И МестныйКэш.Ини.Свойство("Сервис") Тогда
		
		ИниСервис = МестныйКэш.ФормаНастроек.Ини(МестныйКэш, "Сервис");
		
		Для Каждого ПунктКоманды Из ИниСервис Цикл
			
			Если ТипЗнч(ПунктКоманды.Значение) = Тип("Структура") И ПунктКоманды.Значение.Свойство("Имя") Тогда
				
				НовыйПунктКоманды 		   = ЭтаФорма.Таблица_Сервис.Добавить();
				НовыйПунктКоманды.Ключ 	   = ПунктКоманды.Ключ;
				НовыйПунктКоманды.Имя 	   = ПунктКоманды.значение.Имя;
				НовыйПунктКоманды.Описание = ПунктКоманды.значение.Описание;
				НовыйПунктКоманды.Команда  = ПунктКоманды.значение.Значение;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Сбросим начальную страницу
	ГруппаНастройкиСтраницы.ТекущаяСтраница = МестныйКэш.ГлавноеОкно.сбисПолучитьСтраницу(ГруппаНастройкиСтраницы, "ОбщиеНастройки"); 
КонецПроцедуры

&НаКлиенте                                             
Процедура ОбновитьДоступностьНастроек()
// Процедура устанавливает видимость элементов формы в зависимости от выбранного варианта настроек прокси		

	ДоступностьПрокси = ТипПрокси = "Вручную";
	СбисЭлементВидимость = МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "ИнтеграцияAPIВызовыНаКлиенте");
	Если Не СбисЭлементВидимость = Неопределено Тогда
		#Если ВебКлиент Тогда
			СбисЭлементВидимость.Видимость = Ложь;
		#Иначе
			СбисЭлементВидимость.Видимость = (СпособОбмена = 3);
		#КонецЕсли
	КонецЕсли;
	
	МестныйКэш.ГлавноеОкно.СбисЭлементФормы(ЭтаФорма, "ШифроватьВыборочно").Видимость	= СпособОбмена = 5 Или СпособОбмена = 7;    

	МестныйКэш.ГлавноеОкно.СбисЭлементФормы(ЭтаФорма, "ВремяОжиданияОтвета").Видимость	= СпособОбмена = 0 Или СпособОбмена = 7 
																  Или СпособОбмена = 6 Или СпособОбмена = 5 Или СпособОбмена = 4;
																  
	МестныйКэш.ГлавноеОкно.СбисЭлементФормы(ЭтаФорма, "КаталогОбмена").Видимость		= СпособОбмена = 1;
	МестныйКэш.ГлавноеОкно.СбисЭлементФормы(ЭтаФорма, "НастройкаЭП").Видимость			= СпособОбмена = 3;
	МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "КаталогОтладки").Видимость 		= РежимОтладки;
	
	МестныйКэш.ГлавноеОкно.СбисЭлементФормы(ЭтаФорма, "ИспользоватьГенератор").Видимость = МестныйКэш.СБИС.ПараметрыИнтеграции.ГенераторФЭД;
	МестныйКэш.ГлавноеОкно.СбисЭлементФормы(ЭтаФорма, "КаталогНастроек").Видимость		= СпособХраненияНастроек = 0;
	
	МестныйКэш.ГлавноеОкно.СбисЭлементФормы(ЭтаФорма, "ПроксиСервер").Доступность		= ДоступностьПрокси;
	МестныйКэш.ГлавноеОкно.СбисЭлементФормы(ЭтаФорма, "ПроксиПорт").Доступность			= ДоступностьПрокси;
	МестныйКэш.ГлавноеОкно.СбисЭлементФормы(ЭтаФорма, "ПроксиЛогин").Доступность		= ДоступностьПрокси;
	МестныйКэш.ГлавноеОкно.СбисЭлементФормы(ЭтаФорма, "ПроксиПароль").Доступность		= ДоступностьПрокси;
	
	Если Не МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда 
		МестныйКэш.ГлавноеОкно.СбисЭлементФормы(ЭтаФорма, "НадписьСервер").Доступность	= ДоступностьПрокси;
		МестныйКэш.ГлавноеОкно.СбисЭлементФормы(ЭтаФорма, "НадписьПорт").Доступность	= ДоступностьПрокси;
		МестныйКэш.ГлавноеОкно.СбисЭлементФормы(ЭтаФорма, "НадписьЛогин").Доступность	= ДоступностьПрокси;
		МестныйКэш.ГлавноеОкно.СбисЭлементФормы(ЭтаФорма, "НадписьПароль").Доступность	= ДоступностьПрокси;
	КонецЕсли;
	
	Если	ДоступностьПрокси
		И	МестныйКэш.Парам.Свойство("ПроксиСервер")
		И	СокрЛП(МестныйКэш.Парам.ПроксиСервер) <> "" Тогда
		
		ПроксиСервер = МестныйКэш.Парам.ПроксиСервер;
	КонецЕсли;	 
	
	//Определить возможность использования серверных настроек для интеграции
	ВременныйКэш = МодульОбъектаКлиент().НовыйЛокальныйКэш(Новый Структура("ТихийРежим", Истина));
	МестныйКэш.ГлавноеОкно.ОпределитьФормуИнтеграции(ВременныйКэш, СпособОбмена);

	Если	ВременныйКэш.Интеграция.ДоступныСерверныеНастройки()	Тогда

		МестныйКэш.ГлавноеОкно.СбисЭлементФормы(ЭтаФорма, "СпособХраненияНастроек").ТолькоПросмотр	= Ложь;

	Иначе 

		СпособХраненияНастроек	= 0; 
		МестныйКэш.ГлавноеОкно.СбисЭлементФормы(ЭтаФорма, "СпособХраненияНастроек").ТолькоПросмотр	= Истина;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СбисВыбратьКаталог(СтароеЗначение)
	
	ДиалогОткрытия = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога); 
	ДиалогОткрытия.Каталог		= СтароеЗначение;
	ДиалогОткрытия.Заголовок	= "Выберите каталог"; 
	Если ДиалогОткрытия.Выбрать() Тогда 
		Возврат ДиалогОткрытия.Каталог + "\"; 
	КонецЕсли;
	
	Возврат	СтароеЗначение;
	
КонецФункции

//Заполняет список адресов сервера
&НаКлиенте
Процедура СбисЗаполнитьАдресСервера(Кэш)
	
	ДанныеЭлементов = "ЭлементыФормы";
	Если Кэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
		ДанныеЭлементов = "Элементы";
	КонецЕсли;
	ЭтаФорма[ДанныеЭлементов].АдресСервера.СписокВыбора.Очистить();
	
	СписокСерверов		= Кэш.ГлавноеОкно.СбисСписокСерверов(Кэш);
	ПредставлениеАдрес	= СтрЗаменить(СтрЗаменить(СокрЛП(АдресСервера), "https:", ""), "/", "");
	ДобавитьАдрес		= Истина;
	Для Каждого ЭлементСписка Из СписокСерверов Цикл
		ЭтаФорма[ДанныеЭлементов].АдресСервера.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
		Если ЭлементСписка.Представление = ПредставлениеАдрес Тогда
			АдресСервера = ЭлементСписка.Значение;
			ДобавитьАдрес = Ложь;
		КонецЕсли;
	КонецЦикла;
	Если ДобавитьАдрес Тогда
		ЭтаФорма[ДанныеЭлементов].АдресСервера.СписокВыбора.Добавить(АдресСервера, ПредставлениеАдрес);
	КонецЕсли;
	АдресСервера = АдресСервера;//перевыберем для списка
	
КонецПроцедуры

// Процедура записывает выбранные настройки в сохраняемые параметры и закрывает форму	
&НаКлиенте
Процедура СбисПрименитьНастройки()
	
	Если СпособОбмена = 1 И Не ЗначениеЗаполнено(КаталогОбмена) Тогда
		ТекстСообщения = "Заполните каталог обмена документами";
		Если МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда 	
			Сообщить(ТекстСообщения);
		Иначе
			Предупреждение(ТекстСообщения);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(МестныйКэш.Парам, ЭтаФорма, "СпособОбмена, СпособХраненияНастроек, КаталогОбмена, КаталогНастроек, ВремяОжиданияОтвета, ШифроватьВыборочно, АдресСервера");
	Если ТипПрокси = "Вручную" Тогда
		МестныйКэш.Парам.Вставить("ТипПрокси","Вручную");
		МестныйКэш.Парам.Вставить("ПроксиСервер",	ПроксиСервер);
		МестныйКэш.Парам.Вставить("ПроксиПорт",		ПроксиПорт );
		МестныйКэш.Парам.Вставить("ПроксиЛогин",	ПроксиЛогин);
		МестныйКэш.Парам.Вставить("ПроксиПароль",	ПроксиПароль);
	ИначеЕсли ТипПрокси = "Автоматически" Тогда
		МестныйКэш.Парам.Вставить("ТипПрокси","Автоматически");
	Иначе
		МестныйКэш.Парам.Вставить("ТипПрокси","НеИспользовать");
	КонецЕсли;
	#Если Не ТолстыйКлиентОбычноеПриложение Тогда
		МестныйКэш.Парам.ИнтеграцияAPIВызовыНаКлиенте = ИнтеграцияAPIВызовыНаКлиенте;
    #КонецЕсли
	МестныйКэш.Интеграция.СбисУстановитьВремяОжидания(МестныйКэш, ВремяОжиданияОтвета);
	МестныйКэш.ГлавноеОкно.АдресСервера = АдресСервера;
	МестныйКэш.СБИС.АдресСервера = АдресСервера;
	МестныйКэш.СБИС.ПараметрыИнтеграции.РезервныйДомен = МестныйКэш.Интеграция.сбисВключенРезервныйДомен(МестныйКэш, АдресСервера);
	
	НажатиеСохранитьВыполнено = Истина;
	
	ЭтаФорма.Закрыть(МестныйКэш.Парам);	
	
КонецПроцедуры

//Выполняет вызов проверки автообновления
&НаКлиенте
Процедура СбисПроверитьНаличиеОбновлений(ПараметрыПроверки, ДоПараметры = Неопределено) Экспорт
	
	Попытка
		Если ЗначениеЗаполнено(МестныйКэш.Парам.ОжидаемаяВерсия) Тогда
			Сообщить("На текущий момент обновление не требуется - установлена актуальная версия внешней обработки");
			Возврат;
		КонецЕсли;
		
		МестныйКэш.ГлавноеОкно.сбисПоказатьСостояние("Проверка наличия обновлений");
		МестныйКэш.ОбщиеФункции.СбисПроверитьНаличиеОбновлений(МестныйКэш, ПараметрыПроверки);
		МестныйКэш.ГлавноеОкно.сбисСпрятатьСостояние();
		
	Исключение
		ИнфоОбОшибке = ИнформацияОбОшибке();
		МодульОбъектаКлиент().ВызватьСбисИсключение(ИнфоОбОшибке, "ФормаГлавноеОкно.СбисПроверитьНаличиеОбновлений");
	КонецПопытки;

КонецПроцедуры

&НаКлиенте
Процедура СбисУстановитьФорму(ПараметрыОткрытия)
		
	ПараметрыФормы = Новый Структура();
	
	Если		ПараметрыОткрытия = Неопределено Тогда
		РежимЗапуска = "ОбщиеНастройки";
	Иначе
		Если Не	ПараметрыОткрытия.Свойство("РежимЗапуска", РежимЗапуска) Тогда
			РежимЗапуска = "ОбщиеНастройки";
		КонецЕсли;
		Если ПараметрыОткрытия.Свойство("Кэш") Тогда
			МестныйКэш = ПараметрыОткрытия.Кэш;
		КонецЕсли;
	КонецЕсли;
	
	Если МестныйКэш = Неопределено Тогда
		МестныйКэш = ВладелецФормы.Кэш;
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(ЭтаФорма, МестныйКэш.Парам);
	
	ЭлементДействия = МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭтаФорма, "НастройкиСтраницы");
	
	Если РежимЗапуска = "ОбщиеНастройки" Тогда
		
		ЗаполнитьВкладкуСервис();
		ЗаполнитьЗначенияСвойств(ЭтаФорма, МестныйКэш.ГлавноеОкно, "ДатаПоследнегоЗапросаСтатусов, ИдентификаторПоследнегоСобытия, ДатНачЧтенияСтатусов, ДатКнцЧтенияСтатусов");
		
		МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭлементДействия, "СтраницаСоединение").Видимость   = Истина;
		МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭлементДействия, "НастройкиДокументов").Видимость  = Истина;
		МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭлементДействия, "ОбщиеНастройки").Видимость  	   = Истина;
		МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭлементДействия, "Сервис").Видимость  			   = Ложь;
		
	ИначеЕсли РежимЗапуска = "НастройкиСоединения" Тогда   
		
		РежимЗапускаНастройкаСоединения();
		
	ИначеЕсли РежимЗапуска = "Сервисы" Тогда
		
		МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭлементДействия, "СтраницаСоединение").Видимость   = Ложь;
		МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭлементДействия, "НастройкиДокументов").Видимость  = Ложь;
		МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭлементДействия, "ОбщиеНастройки").Видимость  	   = Ложь;
		МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭлементДействия, "Сервис").Видимость  			   = Истина;	
		ЗаполнитьВкладкуСервис();
		
	Иначе
		
		ИнфоОбОшибке = "Передано неизвестное значение параметра ""Режим запуска""";
		МодульОбъектаКлиент().ВызватьСбисИсключение(ИнфоОбОшибке, "ФормаНастройки.ПриОткрытии")

	КонецЕсли;   
	
	Если НЕ МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "НадписьАдресСервисаОбновлений") = Неопределено Тогда 
		МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "НадписьАдресСервисаОбновлений").Видимость = РежимОтладки;   
	КонецЕсли;	
	МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "АдресСервисаОбновлений").Видимость = РежимОтладки;

	ОбновитьДоступностьНастроек();
	
КонецПроцедуры

//Процедура управляет включением/отключением вывода логов
&НаКлиенте
Процедура СбисПереключитьОтладку()
	Если РежимОтладки Тогда
		МестныйКэш.Интеграция.ВключитьОтладку(МестныйКэш, КаталогОтладки);
	Иначе
		МестныйКэш.Интеграция.ОтключитьОтладку(МестныйКэш);
	КонецЕсли;
	
	МестныйКэш.Парам.РежимОтладки	= РежимОтладки;
	МестныйКэш.Парам.КаталогОтладки = КаталогОтладки;
	МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "КаталогОтладки").Видимость = РежимОтладки;
	//+++ МАИ 09.09.2021 Переопределяем сервер обновлений, если пользователь в режиме отладки указал другой
	МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "АдресСервисаОбновлений").Видимость = РежимОтладки;
	Если НЕ РежимОтладки ИЛИ АдресСервисаОбновлений = "" Тогда
		АдресСервисаОбновлений = "update.sbis.ru";
		МестныйКэш.СБИС.ПараметрыИнтеграции.Вставить("АдресСервисаОбновлений", АдресСервисаОбновлений);
	КонецЕсли;
	//--- МАИ 09.09.2021
КонецПроцедуры					

&НаКлиенте
Процедура РежимЗапускаНастройкаСоединения()
	
	ЭлементДействия = МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "НастройкиСтраницы");
	МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭлементДействия, "СтраницаСоединение").Видимость   = Истина;
	МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭлементДействия, "НастройкиДокументов").Видимость  = Ложь;
	МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭлементДействия, "ОбщиеНастройки").Видимость  	   = Ложь;
	МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭлементДействия, "Сервис").Видимость  			   = Ложь;	
	СбисЗаполнитьАдресСервера(МестныйКэш);
	ЗаполнитьЗначенияСвойств(НастройкиПодключения_Было, ЭтаФорма);
	
	СпособОбменаСписокВыбора = МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "СпособОбмена").СписокВыбора;
	Если СпособОбмена = 0 И ПустаяСтрока(СпособОбменаСписокВыбора.НайтиПоЗначению(0))	Тогда
		
		СпособОбменаСписокВыбора.Вставить(0, 0, "SDK");
		Попытка 
			МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "СпособОбмена").Значение = СпособОбменаСписокВыбора.Получить(0).Значение;  //по индексу - SDK
		Исключение	
			
		КонецПопытки;		
		
		СпособОбменаСписокВыбора.СортироватьПоЗначению();
	КонецЕсли;
	
	Если СпособОбмена = 4 И ПустаяСтрока(СпособОбменаСписокВыбора.НайтиПоЗначению(4))	Тогда
		СпособОбменаСписокВыбора.Вставить(3, 4, "ExtSDK");
		Попытка 
			МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "СпособОбмена").Значение = СпособОбменаСписокВыбора.Получить(3).Значение;  //по индексу - ExtSDK
		Исключение	
			
		КонецПопытки;		
		
		СпособОбменаСписокВыбора.СортироватьПоЗначению();
	КонецЕсли;
	
	Если СпособОбмена = 5 И ПустаяСтрока(СпособОбменаСписокВыбора.НайтиПоЗначению(5))	Тогда
		СпособОбменаСписокВыбора.Вставить(4, 5, "ExtSDKCrypto");
		Попытка 
			МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "СпособОбмена").Значение = СпособОбменаСписокВыбора.Получить(4).Значение;	//по индексу - ExtSDKCrypto	
		Исключение	
			
		КонецПопытки;	
		
		СпособОбменаСписокВыбора.СортироватьПоЗначению();
	КонецЕсли;
	
	Если ТипПрокси = Неопределено Тогда
		ТипПрокси = "НеИспользовать";
		
	ИначеЕсли Не(	ТипПрокси = "Вручную"
		Или	ТипПрокси = "НеИспользовать") Тогда;
		
		ТипПрокси = "Автоматически";
	КонецЕсли;
	
	Если Не ТипПрокси = "Вручную" Тогда
		ПроксиСервер = "";
		ПроксиПорт   = ""; 
		ПроксиЛогин  = "";  
		ПроксиПароль = "";
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

КлючиИспользовать = "СпособОбмена, СпособХраненияНастроек, КаталогНастроек, КаталогОбмена, АдресСервера, ШифроватьВыборочно, ВремяОжиданияОтвета";
НастройкиПодключения_Было = Новый Структура(КлючиИспользовать);
#Если Не ТолстыйКлиентОбычноеПриложение Тогда
	НастройкиПодключения_Было.Вставить("ИнтеграцияAPIВызовыНаКлиенте");
#КонецЕсли

