Функция СведенияОВнешнейОбработке() Экспорт
	
	МассивНазначений = Новый Массив;
	МассивНазначений.Добавить("Документ.ПроизводствоБезЗаказа");          
	
	ПараметрыРегистрации = Новый Структура;
	ПараметрыРегистрации.Вставить("Вид", "ПечатнаяФорма");
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
	ПараметрыРегистрации.Вставить("Наименование", "ОИР Списание МТР на выпуск продукции");
	ПараметрыРегистрации.Вставить("Версия", "1.0");
	ПараметрыРегистрации.Вставить("Информация", "ОИР");
	ПараметрыРегистрации.Вставить("ВерсияБСП", "2.3.4.5");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Истина);
	Команды = ТаблицаКоманд();
	ДобавитьКоманду(Команды, 
	"ОИР Списание МТР на выпуск продукции",
	"ПГЗ",
	"ВызовСерверногоМетода",
	Истина,
	"ПечатьMXL");
	ПараметрыРегистрации.Вставить("Команды", Команды);
	Возврат ПараметрыРегистрации; 
	
КонецФункции

Функция ТаблицаКоманд()
	
	Команды = Новый ТаблицаЗначений;
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	Возврат Команды;
	
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПГЗ") Тогда
		СтруктураПараметровПечати = Новый Структура;
		СтруктураПараметровПечати.Вставить("ВидДокументаПечати",  "СписаниеМатериаловИзЭксплуатации");
		СтруктураПараметровПечати.Вставить("ИмяПараметровПечати", "ПАРАМЕТРЫ_ПЕЧАТИ_ПГЗ");
		СтруктураПараметровПечати.Вставить("ИмяМакетаПечати",     "ПГЗ");
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,"ПГЗ", "Передача материалов (По группе затрат)", 
		ПечатьПГЗ(МассивОбъектов, ОбъектыПечати, СтруктураПараметровПечати),,);
	КонецЕсли;
	
КонецПроцедуры

// Функция формирует печатную форму документа
//
Функция ПечатьПГЗ(МассивОбъектов, ОбъектыПечати, СтруктураПараметровПечати)
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПГЗ";
	Макет = ПолучитьМакет("ПГЗ"); 
	
	ПервыйНомерСтроки = 18;
	
	Для каждого ОбъектДокумента из МассивОбъектов Цикл
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПроизводствоБезЗаказаМатериалыИРаботы.Склад КАК Склад,
		|	ПроизводствоБезЗаказаМатериалыИРаботы.Ссылка.Номер КАК Номер,
		|	ПроизводствоБезЗаказаМатериалыИРаботы.Ссылка.Дата КАК Дата,
		|	ПроизводствоБезЗаказаМатериалыИРаботы.Ссылка.Организация КАК Организация,
		|	ПроизводствоБезЗаказаМатериалыИРаботы.Ссылка.Подразделение КАК Подразделение,
		|	ПроизводствоБезЗаказаМатериалыИРаботы.Ссылка.Ссылка КАК Ссылка,
		|	ПроизводствоБезЗаказаМатериалыИРаботы.ГруппаЗатратДоп КАК ГруппаЗатрат,
		|	ПроизводствоБезЗаказаМатериалыИРаботы.Номенклатура КАК Номенклатура,
		|	ПроизводствоБезЗаказаМатериалыИРаботы.Количество КАК Количество,
		|	ПроизводствоБезЗаказаМатериалыИРаботы.Упаковка КАК Упаковка,
		|	ПроизводствоБезЗаказаМатериалыИРаботы.Склад.ТекущийОтветственный КАК СкладТекущийОтветственный,
		|	ПроизводствоБезЗаказаМатериалыИРаботы.Ссылка.Ответственный КАК Ответственный
		|ИЗ
		|	Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК ПроизводствоБезЗаказаМатериалыИРаботы
		|ГДЕ
		|	ПроизводствоБезЗаказаМатериалыИРаботы.Ссылка = &Ссылка
		|ИТОГИ ПО
		|	Склад,
		|	ГруппаЗатрат";
		Запрос.УстановитьПараметр("Ссылка", ОбъектДокумента.Ссылка);  
		
		Результат = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
		
		
		Пока Результат.Следующий() Цикл 
			
			ТекСклад = Результат.Склад;
			ШапкаВыведена = Ложь;  
			НомерСтроки = 0; 

		
		ВыборкаСклад = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСклад.Следующий() Цикл
			
			ТекГруппа = ВыборкаСклад.ГруппаЗатрат;

			
			ВыборкаГруппаЗатрат = ВыборкаСклад.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаГруппаЗатрат.Следующий() Цикл
													
					Если НЕ ШапкаВыведена Тогда	
						ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
						ОбластьЗаголовок.Параметры.Организация = ВыборкаГруппаЗатрат.Организация;  
						ТекОтветственный = ВыборкаГруппаЗатрат.Ответственный; 
						ТекОтвПоСкладу = ВыборкаГруппаЗатрат.СкладТекущийОтветственный;
						ОбластьЗаголовок.Параметры.Склад = ТекСклад;
						ОбластьЗаголовок.Параметры.Подразделение = ВыборкаГруппаЗатрат.Подразделение; 
						ОбластьЗаголовок.Параметры.ТекстЗаголовка = "Производство без заказа № " + ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаГруппаЗатрат.Номер) +
						" от " + Формат(ВыборкаГруппаЗатрат.Дата, "ДФ=dd.MM.yyyy");  
						ТабДокумент.Вывести(ОбластьЗаголовок); 
						ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
						ТабДокумент.Вывести(ОбластьШапкаТаблицы); 
						ШапкаВыведена = Истина;
					КонецЕсли;                    
			
					
					ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
					ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки + 1; 
					ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры, ВыборкаГруппаЗатрат); 
					Если ПустаяСтрока(ОбластьСтрока.Параметры.Упаковка) Тогда
						ОбластьСтрока.Параметры.Упаковка = ВыборкаГруппаЗатрат.Номенклатура.ЕдиницаИзмерения;
					КонецЕсли;	
					ТабДокумент.Вывести(ОбластьСтрока); 

					
					
				КонецЦикла; 
				
			КонецЦикла; 
			ОбластьПодписи = Макет.ПолучитьОбласть("Подписи"); 
			ОбластьПодписи.Параметры.Отпустил = ФизическиеЛицаЗарплатаКадрыКлиентСервер.ФамилияИнициалы(Строка(ТекОтветственный));  
			ОбластьПодписи.Параметры.Получил = ФизическиеЛицаЗарплатаКадрыКлиентСервер.ФамилияИнициалы(Строка(ТекОтвПоСкладу));
		    ТабДокумент.Вывести(ОбластьПодписи);
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
			ТабДокумент.АвтоМасштаб = Истина;
			
		КонецЦикла;
		
		
	КонецЦикла;	

	
	Возврат ТабДокумент;
	
КонецФункции

