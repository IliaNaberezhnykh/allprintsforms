
&НаКлиенте
Функция ОсновнаяФорма()
	
	Результат = ВладелецФормы.ОсновнаяФорма();
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьФорму(Параметры);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФорму(ПараметрыФормы)
	
	Для Каждого ДопСведение Из ПараметрыФормы.ДопСведенияИзменяемые Цикл

		СтрокаТЧ = ДополнительныеСведения.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, ДопСведение); 
		
	КонецЦикла;
	
	Для Каждого ДопСведение Из ПараметрыФормы.ДопСведенияНеизменяемые Цикл
		
		СтрокаТЧ = ДополнительныеСведенияНеизменяемые.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, ДопСведение); 
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеСведенияЗначениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДополнительныеСведения.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		МаксимальнаяДлинаЗначения = 2000;
		ТекущиеДанные.Value = Лев(ТекущиеДанные.Value, МаксимальнаяДлинаЗначения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Отказ = Ложь;
	
	ПроверкаЗаполненияДанных(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДополнительныхСведений = ДанныеДополнительныхСведений();
	
    Закрыть(ДанныеДополнительныхСведений);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаЗаполненияДанных(Отказ)
	
	Для Каждого СтрокаТЧ Из ДополнительныеСведения Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Key)
			ИЛИ НЕ ЗначениеЗаполнено(СтрокаТЧ.Value) Тогда
			
			ТекстСообщения = НСтр("ru = 'Не заполнены поля в дополнительных сведениях!'");
			ОсновнаяФорма().ВывестиСообщениеПользователю(ТекстСообщения,, ЭтаФорма);
			Отказ = Истина;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Функция ДанныеДополнительныхСведений()
	
	Результат = Новый Массив;
	
	ДобавитьДанныеДополнительныхСведений(Результат, ДополнительныеСведенияНеизменяемые);
	ДобавитьДанныеДополнительныхСведений(Результат, ДополнительныеСведения);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьДанныеДополнительныхСведений(ДопСведения, ТабЧасть)
	
	КлючиДопСведений = "Key, Value";
	
	Для Каждого СтрокаТЧ Из ТабЧасть Цикл
		
		ДопСведение = Новый Структура(КлючиДопСведений);
		ЗаполнитьЗначенияСвойств(ДопСведение, СтрокаТЧ);
		ДопСведения.Добавить(ДопСведение);
		
	КонецЦикла;
	
КонецПроцедуры

