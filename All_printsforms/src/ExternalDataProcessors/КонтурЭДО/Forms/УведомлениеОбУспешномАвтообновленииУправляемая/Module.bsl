
&НаСервере
Перем ОбработкаОбъект;

//{		Сервисные методы

&НаСервере
Функция ОбработкаОбъект()
	
	Если ОбработкаОбъект = Неопределено Тогда
		
		СтруктураОбработки = ПолучитьИзВременногоХранилища(Объект.ОбщийКонтекстКлиентСервер.АдресОбработкаОбъект);
		Если Не СтруктураОбработки = Неопределено Тогда
			ОбработкаОбъект = СтруктураОбработки.ОбработкаОбъект;
		КонецЕсли;
		
		Если ОбработкаОбъект = Неопределено Тогда
			
			ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
			Попытка
				ПоместитьВоВременноеХранилище(Новый Структура("ОбработкаОбъект", ОбработкаОбъект), Объект.ОбщийКонтекстКлиентСервер.АдресОбработкаОбъект);
			Исключение
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат ОбработкаОбъект;
	
КонецФункции

&НаСервере
Функция Модуль_ЯдроНаСервере()
	
	Результат = ОбработкаОбъект().Модуль_Ядро();
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Результат = ВладелецФормы.ОсновнаяФорма();
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция Модуль_ЯдроНаКлиенте()
	
	Результат = ОсновнаяФорма().Модуль_ЯдроНаКлиенте();
	
	Возврат Результат;
	
КонецФункции

//}		Сервисные методы


//{		Обработчики

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Объект.Свойство("ОбщийКонтекстКлиентСервер", Объект.ОбщийКонтекстКлиентСервер);
	
	УстановитьСвойстваФормыТакси();
	
	ЗаполнитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНажатие(Элемент)
	
	Видимость = Не Элементы.ОписаниеИзменений.Видимость;
	Элементы.ОписаниеИзменений.Видимость = Видимость;
	Элементы.ДекорацияОтступСнизу.Видимость = НЕ Видимость;
	
	Метрика_ЗаписатьНажатиеКнопки(Видимость);
		
КонецПроцедуры

//}		Обработчики

&НаСервере
Процедура УстановитьСвойстваФормыТакси()

	НастройкиЭлемента = Новый Структура;
	НастройкиЭлемента.Вставить("Видимость"		, Ложь);
	НастройкиЭлемента.Вставить("ГоризонтальноеПоложениеВГруппе", ГоризонтальноеПоложениеЭлемента.Центр);
	ЗаполнитьЗначенияСвойств(Элементы.ОписаниеИзменений, НастройкиЭлемента);
	
	НастройкиЭлемента = Новый Структура;
	НастройкиЭлемента.Вставить("Загловок"		, "Закрыть");
	НастройкиЭлемента.Вставить("Отображение"	, ОтображениеКнопки.КартинкаИТекст);
	ЗаполнитьЗначенияСвойств(Элементы.Закрыть, НастройкиЭлемента);
	
	НастройкиЭлемента = Новый Структура;
	НастройкиЭлемента.Вставить("РастягиватьПоГоризонтали", Истина);
	НастройкиЭлемента.Вставить("АвтоМаксимальнаяШирина", Ложь);
	НастройкиЭлемента.Вставить("ГоризонтальноеПоложение", ГоризонтальноеПоложениеЭлемента.Центр);
	ЗаполнитьЗначенияСвойств(Элементы.Картинка	, НастройкиЭлемента);
	ЗаполнитьЗначенияСвойств(Элементы.Описание1	, НастройкиЭлемента);
	ЗаполнитьЗначенияСвойств(Элементы.Описание2	, НастройкиЭлемента);
	ЗаполнитьЗначенияСвойств(Элементы.ДекорацияОтступСнизу	, НастройкиЭлемента);

	НастройкиЭлемента = Новый Структура;
	НастройкиЭлемента.Вставить("РастягиватьПоГоризонтали", Ложь);
	НастройкиЭлемента.Вставить("ГоризонтальноеПоложение", ГоризонтальноеПоложениеЭлемента.Центр);
	ЗаполнитьЗначенияСвойств(Элементы.Гиперссылка, НастройкиЭлемента);
	
	НастройкиЭлемента = Новый Структура;
	НастройкиЭлемента.Вставить("Отображение", ОтображениеОбычнойГруппы.Нет);
	ЗаполнитьЗначенияСвойств(Элементы.Группа1 		, НастройкиЭлемента);
	ЗаполнитьЗначенияСвойств(Элементы.Группа2		, НастройкиЭлемента);
	ЗаполнитьЗначенияСвойств(Элементы.Группа3		, НастройкиЭлемента);
	ЗаполнитьЗначенияСвойств(Элементы.Группа4		, НастройкиЭлемента);
	ЗаполнитьЗначенияСвойств(Элементы.Группа5		, НастройкиЭлемента);
	ЗаполнитьЗначенияСвойств(Элементы.Группа6		, НастройкиЭлемента);
	ЗаполнитьЗначенияСвойств(Элементы.Группа7		, НастройкиЭлемента);
	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьДанныеФормы()
	
	Ядро = Модуль_ЯдроНаСервере();
	
	BaseID = Ядро.ОбщиеНастройки_Прочитать("ИдентификаторБазы1С");
	BoxID  = Ядро.ИдентификаторыЯщиковСопоставленныхОрганизаций();
		
	// тип Массив не допустим в данных формы, поэтому:
	СписокBoxID.Очистить();
	СписокBoxID.ЗагрузитьЗначения(BoxID);
	
	ОписаниеИзменений = ОбработкаОбъект().ОписаниеИзмененийТекущейВерсииМодуляHTML();
	
КонецПроцедуры

//{		Метрика

&НаКлиенте
Процедура Метрика_ЗаписатьНажатиеКнопки(Значение)
	
	Ядро = Модуль_ЯдроНаКлиенте();
	
	Категория = Ядро.Метрика_НазваниеКатегории().РезультатОбновленияМодуля;
	Действие = Ядро.Метрика_НазваниеДействий().ПросмотрИзменений;
	НазваниеФормы = Метрика_НазваниеФормы();
	
	Ядро.Метрика_ДобавитьПоведение_НажатиеКнопки(НазваниеФормы, Категория, Действие);
	
	BoxID = СписокBoxID.ВыгрузитьЗначения();
		
	Переменные = Новый Соответствие;
	Переменные.Вставить("BoxID", BoxID);
	Переменные.Вставить("BaseID", BaseID);
	Переменные.Вставить("Значение", Значение);
	
	Ядро.Метрика_ЗаписатьСтатистику(Категория, Действие, , Переменные);
	
КонецПроцедуры

&НаКлиенте
Функция Метрика_НазваниеФормы()

	Результат = "Уведомление об успешном автообновлении";
	
	Возврат Результат;
	
КонецФункции
	
//}		Метрика