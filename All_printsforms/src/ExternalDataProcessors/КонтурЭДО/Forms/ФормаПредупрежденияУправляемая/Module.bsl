&НаСервере
Перем ОбработкаОбъект;

&НаСервере
Функция ОбработкаОбъект()

	Если ОбработкаОбъект = Неопределено Тогда
		
		СтруктураОбработки = ПолучитьИзВременногоХранилища(Объект.ОбщийКонтекстКлиентСервер.АдресОбработкаОбъект);
		Если Не СтруктураОбработки = Неопределено Тогда
			ОбработкаОбъект = СтруктураОбработки.ОбработкаОбъект;
		КонецЕсли;
		
		Если ОбработкаОбъект = Неопределено Тогда
			
			ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
			Попытка
				ПоместитьВоВременноеХранилище(Новый Структура("ОбработкаОбъект", ОбработкаОбъект), Объект.ОбщийКонтекстКлиентСервер.АдресОбработкаОбъект);
			Исключение
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат ОбработкаОбъект;
	
КонецФункции

&НаСервере
Функция Модуль_ЯдроНаСервере()
	
	Результат = ОбработкаОбъект().Модуль_Ядро();
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
	Параметры.Объект.Свойство("ОбщийКонтекстКлиентСервер", Объект.ОбщийКонтекстКлиентСервер);
	ИнициализироватьФорму(Параметры);
	УстановитьОбязательныеСвойстваЭлементов();
	УстановитьВидимостьПоляКомментария();
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьФорму(ПараметрыФормы) Экспорт
	
	ИнициализироватьПараметрыФормы(ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьПараметрыФормы(ПараметрыФормы)
	
	ПараметрЗаголовок = "";
	
	ПараметрыФормы.Свойство("Заголовок", ПараметрЗаголовок);
	ПараметрыФормы.Свойство("ТекстПредупреждения", Элементы.ТекстПредупреждения.Заголовок);
	ПараметрыФормы.Свойство("Комментарий", Комментарий);
	
	Комментарий = Json_2_Строка(Комментарий);
	
	Если ЗначениеЗаполнено(ПараметрЗаголовок) Тогда
		Заголовок = ПараметрЗаголовок;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОбязательныеСвойстваЭлементов()
	
	СвойствоАвтоМаксимальнаяШирина = Новый Структура("АвтоМаксимальнаяШирина", Ложь);
	ЗаполнитьЗначенияСвойств(Элементы.ТекстПредупреждения, СвойствоАвтоМаксимальнаяШирина);
	ЗаполнитьЗначенияСвойств(Элементы.ФормаКомментарий, СвойствоАвтоМаксимальнаяШирина);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПоляКомментария() 
	
	ВидимостьПоля = ЗначениеЗаполнено(Комментарий);
	Элементы.ФормаКомментарий.Видимость = ВидимостьПоля;
	МинимальнаяВысотаФормы = 5;
	
	Если ВидимостьПоля Тогда
		
		КоличествоСтрок = СтрЧислоСтрок(Комментарий);
		ЭтаФорма.Высота = МинимальнаяВысотаФормы + КоличествоСтрок * 2;	
		
	Иначе
		
		ЭтаФорма.Высота = МинимальнаяВысотаФормы;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаЗакрыть(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция Json_2_Строка(JSON)
	
	Модуль_Ядро = Модуль_ЯдроНаСервере();
	
	Результат = Модуль_Ядро.Json_2_Строка(JSON);
	
	Возврат Результат;
	
КонецФункции



