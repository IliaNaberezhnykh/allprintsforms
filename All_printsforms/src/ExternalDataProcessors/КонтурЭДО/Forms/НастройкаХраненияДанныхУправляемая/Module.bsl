
//{ СЕРВИСНЫЕ МЕТОДЫ

&НаКлиенте
Функция ОсновнаяФорма() Экспорт
	
	Результат = ВладелецФормы.ОсновнаяФорма();
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция Модуль_ЯдроНаКлиенте()
	
	Результат = ОсновнаяФорма().Модуль_ЯдроНаКлиенте();
	
	Возврат Результат;
	
КонецФункции

//} СЕРВИСНЫЕ МЕТОДЫ


//{ ОСНОВНЫЕ СОБЫТИЯ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Объект.Свойство("ОбщийКонтекстКлиентСервер", Объект.ОбщийКонтекстКлиентСервер);
	
	УстановитьСвойстваФормыТакси();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваФормыТакси()
	
	СвойствоАвтоМаксимальнаяШирина = Новый Структура("АвтоМаксимальнаяШирина", Ложь);
	
	ЗаполнитьЗначенияСвойств(Элементы.Заголовок				, СвойствоАвтоМаксимальнаяШирина);
	ЗаполнитьЗначенияСвойств(Элементы.ТекстДополнительный	, СвойствоАвтоМаксимальнаяШирина);
	ЗаполнитьЗначенияСвойств(Элементы.ТекстОсновной			, СвойствоАвтоМаксимальнаяШирина);
	
	Элементы.ГиперссылкаИнструкции.ЦветТекста = Новый Цвет(83,106,194);
	Элементы.ТекстДополнительный.ЦветТекста = Новый Цвет(89,67,4);
	
КонецПроцедуры

//} ОСНОВНЫЕ СОБЫТИЯ ФОРМЫ

//{	ИНТЕРФЕЙСНЫЕ ОБРАБОТЧИКИ
          
&НаКлиенте
Процедура СохранитьКонфигурациюВФайл(Команда)
	
	КатегорияМетрики	= Метрика_НазваниеКатегории();
	ДействиеМетрики		= "Скачать обновление конфигурации";
	
	Модуль_ЯдроНаКлиенте().Метрика_ДобавитьПоведение_НажатиеКнопки(Метрика_НазваниеФормы(), КатегорияМетрики, ДействиеМетрики);

	#Если ВебКлиент Тогда
		
		Адрес = ПоместитьВоВременноеХранилище(ПолучитьМакетДляОбновленияДляХраненияДанных(), УникальныйИдентификатор);
		ПолучитьФайл(Адрес, "Диадок_upd.cf", Истина);
		СохранитьКонфигурациюВФайлОкончание();
			
	#Иначе	
	
		ОписаниеОповещения = ОсновнаяФорма().НовыйОписаниеОповещения("СохранениеКонфигурацииВФайлПутьОкончаниеВыбора", ЭтаФорма);
		
		Режим = РежимДиалогаВыбораФайла.Сохранение;
		
		ПараметрыВыбора = Новый Структура;
		ПараметрыВыбора.Вставить("Фильтр", "Файл конфигурации 1С (*.cf)|*.cf");
		ПараметрыВыбора.Вставить("Заголовок", "Укажите файл, в который нужно сохранить обновление для вашей конфигурации");
		ПараметрыВыбора.Вставить("ПолноеИмяФайла", "Диадок_upd.cf");
		
		ОсновнаяФорма().ПоказатьДиалогВыбораФайла(ОписаниеОповещения, Режим, ПараметрыВыбора);
		
	#КонецЕсли
			
КонецПроцедуры

&НаКлиенте
Процедура СохранениеКонфигурацииВФайлПутьОкончаниеВыбора(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт 
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКФайлу = ВыбранныеФайлы[0];
			
	ПолучитьМакетДляОбновленияДляХраненияДанных().Записать(ПутьКФайлу);
	
	СохранитьКонфигурациюВФайлОкончание();
		
КонецПроцедуры

&НаСервере
Функция ПолучитьМакетДляОбновленияДляХраненияДанных()
		
	Возврат РеквизитФормыВЗначение("Объект").ПолучитьМакет("ОбновлениеДляХраненияДанных_cf");
	
КонецФункции

&НаКлиенте
Процедура СохранитьКонфигурациюВФайлОкончание()
	
	ОписаниеОповещения = ОсновнаяФорма().НовыйОписаниеОповещения("ОбработчикЗакрытияФормы", ЭтаФорма);
	ОсновнаяФорма().ПоказатьПредупреждениеПереопределенная(ОписаниеОповещения,
	"ВНИМАНИЕ! 
	|При обновлении конфигурации в окне ""Сравнение, объединение"" НЕ ЗАБУДЬТЕ снять флажок с раздела ""Свойства""!
	|Запустите модуль после обновления конфигурации базы из файла.");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикЗакрытияФормы(Параметры) Экспорт
	
	Закрыть();
	
КонецПроцедуры

//} ИНТЕРФЕЙСНЫЕ ОБРАБОТЧИКИ

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Модуль_ЯдроНаКлиенте().Метрика_ДобавитьПоведение_ДействиеСФормой(Метрика_НазваниеФормы(), Метрика_НазваниеКатегории());
	
КонецПроцедуры

&НаКлиенте
Функция Метрика_НазваниеФормы()
	
	Возврат "Настройка хранения данных";
	
КонецФункции

&НаКлиенте
Функция Метрика_НазваниеКатегории()
	
	Возврат "Система хранения";
	
КонецФункции

&НаКлиенте
Процедура ГиперссылкаИнструкцииНажатие(Элемент)
	
	Ядро = Модуль_ЯдроНаКлиенте();
	
	АдресаИнтернетРесурсов = Ядро.Перечисление_АдресаИнтернетРесурсов();
	
	ОсновнаяФорма().ОповеститьИлиПерейтиПоСсылке(АдресаИнтернетРесурсов.Инструкция_УстановкаПодсистемыДД, Неопределено, ЭтаФорма);
	
КонецПроцедуры

